(no-implicit-core)

(include "core/foundation.neut")

(include "core/functor.neut")

(include "core/monad.neut")

(include "core/base.neut")

(include "core/identity.neut")

(inductive free-internal ((g (hom tau tau)) (a tau))
  (leaf
    ((_ a))
    (free-internal g a))
  (node
    ((b tau)
     (_ (g b))
     (_ (hom b (free-internal g a))))
    (free-internal g a)))

(use free-internal)

(define free ((g (hom tau tau)))
  (witness (hom tau tau)
    (lambda (a)
      (free-internal g a))))

(define free.fold
  free-internal.fold)

(section free)

(define map
  ((g (hom tau tau)))
  (witness (functor-form (free g))
    (lambda (a b (f (hom a b)) m)
      (free.fold g a m
        (lambda (_ _)
          ((free g) b))
        (lambda (x)
          (leaf g b (f x)))
        (lambda (p val acc)
          (node g b p val acc))))))

;; f (Free f a) -> Free f aがほしいが。
;; fに構造がないと無理そう？……いけた。
(define in ((f (hom tau tau)) (a tau) (x (f ((free f) a))))
  (witness ((free f) a)
    (free-internal.node
      f
      a
      ((free f) a)
      x
      (lambda (y) y))))

;; Free f a -> f (Free f a)
?free.fold

;; free fがfunctorであることを利用してなんかうまくやれないか？
;; fのところにfree fを自動で与えるようにすればいいだけか？x : free (free f) aとする、ってわけ。
;; その場合は結果のほうはfree f (free (free f) a)みたいになるが。fについてのfoldが使えないから微妙では？
(define out
  ((f (hom tau tau))
   (M (monad f))
   (a tau)
   (x ((free f) a)))
  (witness (f ((free f) a))
    (free.fold f a x
      (lambda (_ _)
        (f ((free f) a)))
      (lambda (x)
        ((functor.map f (monad.witness-functor f M))
           a
           ((free f) a)
           (lambda (val) (free-internal.leaf f a val))
           ((monad.return f M) a x)))
      (lambda (b v k)
        ((functor.map f (monad.witness-functor f M))
          b
          ((free f) a)
          (λ (value) (free.in f a (k value)))
          v)))))

(define witness-functor
  ((g (hom tau tau)))
  (witness (functor (free g))
    (functor.new
      (free g)
      (free.map g))))

(define return
  ((g (hom tau tau)))
  (witness (return-type (free g))
    (lambda (_ x)
      (leaf * * x))))

(define bind
  ((g (hom tau tau)))
  (witness (bind-type (free g))
    (lambda
      ((a tau)
       (b tau)
       (m ((free g) a))
       (k (hom a ((free g) b))))
      (free.fold g a m
        (lambda (_ _)
          ((free g) b))
        (lambda (x)
          (k x))
        (lambda (p val acc)
          (node * * p val acc))))))

(define witness-monad
  ((g (hom tau tau)))
  (witness (monad (free g))
    (monad.new
      (free g)
      (free.witness-functor g)
      (free.return g)
      (free.bind g))))

(provide map)

(provide return)

(provide bind)

(provide witness-functor)

(provide witness-monad)

(end free)
