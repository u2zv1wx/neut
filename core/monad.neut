(no-implicit-core)

(include library "core/foundation.neut")

(include library "core/functor.neut")

(include library "core/top.neut")

(include library "core/bool.neut")

(definition return-type ((m (hom tau tau)))
  (pi
    ((a tau)
     (_ a))
    (m a)))

(definition bind-type ((m (hom tau tau)))
  (pi
    ((a tau)
     (b tau)
     (_ (m a))
     (_ (hom a (m b))))
    (m b)))

(coinductive monad ((m (hom tau tau)))
  (witness-functor
    ((_ (monad m)))
    (functor m))
  (return ;; return-internalのほうがいいかも？
    ((_ (monad m)))
    (return-type m))
  (bind
    ((_ (monad m)))
    (bind-type m)))

(section monad)

(definition return-plus
  ([a tau]
   [m (hom tau tau)]
   (info (monad m)))
  (witness
    (hom a (m a))
    (lambda (x) ((monad:return info) a x))))

(definition bind-plus
  ([a tau]
   [b tau]
   [m (hom tau tau)]
   (info (monad m)))
  (witness
    (forall
      ((x (m a))
       (f (hom a (m b))))
      (m b))
    (lambda (x f)
      ((monad:bind info) a b x f))))

(definition compose
  ([m hole]
   [a hole]
   [b hole]
   [c hole]
   (info (monad m))
   (f (hom a (m b)))
   (g (hom b (m c))))
  (witness (hom a (m c))
    (lambda (x)
      (with (bind-plus info)
        (let tmp (f x))
        (g tmp)))))

(definition when
  ([f (hom tau tau)]
   (info (monad f))
   (condition bool)
   (comp (f top)))
  (witness (f top)
    (if condition
      comp
      ((return-plus info) top:unit))))

(end monad)
