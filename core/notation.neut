(no-implicit-core)

;; define basic notations

;; synonyms
(keyword type)

(notation type tau)

(keyword universe)

(notation universe tau)

(keyword forall)

(notation forall pi)

(keyword switch)

(notation switch enum-elimination)

(keyword hole)

(notation hole (zeta _))

(keyword let)

(keyword begin)

(notation (begin e) e)

(notation (begin e rest+)
  ((pi-introduction ((_ hole)) (begin rest+)) (begin e)))

(notation (begin (let x e+) rest+)
  ((pi-introduction ((x hole)) (begin rest+)) (begin e+)))

(notation (begin (let (x t) e+) rest+)
  ((pi-introduction ((x t)) (begin rest+)) (begin e+)))

(keyword with)

;; (with identity:bind (let x foo) (let y bar) buz (let p qux) result) みたいなのが合法になる。
(notation (with bind e) e)

(notation (with bind e rest+)
  (bind (with bind e) (pi-introduction ((_ hole)) (with bind rest+))))

(notation (with bind (let x e+) rest+)
  (bind (with bind e+) (pi-introduction ((x hole)) (with bind rest+))))

(notation (with bind (let (x t) e+) rest+)
  (bind (with bind e+) (pi-introduction ((x t)) (with bind rest+))))

(keyword lambda)

(notation lambda pi-introduction)

;; lambda: pi-introduction with implicit begin block -> (discard)
;; (notation (lambda () e+)
;;   (pi-introduction () (begin e+)))

;; (notation (lambda (xts+) e+)
;;   (pi-introduction (xts+) (begin e+)))

;; type ascription
(keyword ascription)

(notation (ascription e t)
  (let (_ t) e))

(keyword with-type)

(notation (with-type t e)
  ((lambda ((x t)) x) e))

(keyword witness)

(notation (witness t e)
  ((lambda ((x t)) x) e))

(keyword hom)

(notation (hom t) t)

(notation (hom t rest+)
  (pi
    ((_ t))
    (hom rest+)))

(keyword theorem)

(notation (theorem x t e)
  (let (x t) e))

(keyword assume)

(notation assume lambda)

(keyword tuple)

(notation tuple sigma-introduction)

(keyword construct)

(notation construct sigma-introduction)

(keyword destruct)

(notation (destruct e xts rest+)
  (sigma-elimination xts e (begin rest+)))

(keyword cast)

(notation (cast t e)
  (unsafe-cast hole t e))
